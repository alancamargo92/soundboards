apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.ukdev.carcadasalborghetti"
        minSdkVersion 16
        targetSdkVersion 29
        multiDexEnabled true
    }

    signingConfigs {
        free {
            keyAlias System.getenv("KEY_ALIAS_CARCADAS")
            keyPassword System.getenv("KEY_PASSWORD_CARCADAS")
            storeFile file(System.getenv("STORE_FILE_CARCADAS_FREE"))
            storePassword System.getenv("STORE_PASSWORD_CARCADAS")
        }

        paid {
            keyAlias System.getenv("KEY_ALIAS_CARCADAS")
            keyPassword System.getenv("KEY_PASSWORD_CARCADAS")
            storeFile file(System.getenv("STORE_FILE_CARCADAS_PAID"))
            storePassword System.getenv("STORE_PASSWORD_CARCADAS")
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"

    productFlavors {
        free {
            versionCode 47
            versionName "2020.1.1"
            dimension "version"
            signingConfig signingConfigs.free
        }

        paid {
            versionCode 46
            versionName "2020.1.1"
            dimension "version"
            applicationIdSuffix ".paid"
            buildConfigField "String", "BASE_URL", '"https://api.dropboxapi.com/"'
            buildConfigField "String", "BASE_URL_DOWNLOADS", '"https://content.dropboxapi.com/"'
            buildConfigField "String", "ACCESS_TOKEN", "\"$System.env.ACCESS_TOKEN_CARCADAS\""
            signingConfig signingConfigs.paid
        }
    }

    sourceSets {
        main.java.srcDirs = ['src/main/kotlin']
        free.java.srcDirs = ['src/free/kotlin']
        paid.java.srcDirs = ['src/paid/kotlin']
        test.java.srcDirs = ['src/test/kotlin']
        testFree.java.srcDirs = ['src/testFree/kotlin']
        testPaid.java.srcDirs = ['src/testPaid/kotlin']
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    androidExtensions {
        experimental true
    }

    testOptions {
        unitTests.returnDefaultValues true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha01'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.google.firebase:firebase-core:17.2.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-android-ext:$koin_version"
    implementation "org.koin:koin-androidx-ext:$koin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    freeImplementation 'com.google.firebase:firebase-ads:18.3.0'

    paidImplementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    paidImplementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    paidImplementation 'com.google.code.gson:gson:2.8.6'
    paidImplementation "androidx.room:room-runtime:$room_version"
    paidImplementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    testImplementation 'junit:junit:4.13'
    testImplementation 'io.mockk:mockk:1.9.1'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.2.2'
}

apply plugin: 'com.google.gms.google-services'